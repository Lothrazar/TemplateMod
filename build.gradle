buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mc_version}-${mod_version}"
group = 'com.lothrazar.examplemod' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'examplemod'

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"
    mappings = "stable_39"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    useDepAts = true
    replace '@FINGERPRINT@', project.findProperty('signSHA1')
    replace "@VERSION@", "${mod_version}"
	replaceIn "ExampleMod.java"
}
task signJar(type: SignJar, dependsOn: reobfJar) {

     // Skips if the keyStore property is missing.
    onlyIf {
        project.hasProperty('keyStore')
    }

     // findProperty allows us to reference the property without it existing.
    // Using project.propName would cause the script to fail validation if
    // the property did not exist.
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}
// Runs this task automatically when build is ran.
build.dependsOn signJar

repositories {
    maven { // JEI
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven { //Guide API
        url "http://tehnut.info/maven/"
    }
    maven {
        url = "http://minecraft.curseforge.com/api/maven/"
    }
}
dependencies {
   deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
